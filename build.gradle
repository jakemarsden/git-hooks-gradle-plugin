plugins {
  id 'com.github.sherter.google-java-format' version '0.8'
  id 'java-gradle-plugin'
}

group 'com.jakemarsden'
version file('VERSION').text.trim()

repositories {
  jcenter()
}

ext {
  hamcrestVersion = '2.2'
  junitVersion = '5.6.2'
}

dependencies {
  testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

gradlePlugin {
  plugins {
    gitHooksPlugin {
      id = "com.jakemarsden.git-hooks"
      implementationClass = "com.jakemarsden.githooksgradleplugin.GitHooksPlugin"
    }
  }
}

java {
  sourceCompatibility JavaVersion.VERSION_11
  targetCompatibility JavaVersion.VERSION_11
  withJavadocJar()
  withSourcesJar()
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [
      '-Werror',
      '-Xlint:all',
      '-Xlint:-processing'
  ]
  options.encoding 'UTF-8'
}

tasks.withType(Javadoc) {
  options.encoding 'UTF-8'
}

tasks.withType(Test) {
  useJUnitPlatform()
}

googleJavaFormat {
  toolVersion '1.7'
  options style: 'GOOGLE'
}

task format {
  // alias
  group tasks.googleJavaFormat.group
  dependsOn tasks.googleJavaFormat
}
