plugins {
  id 'com.github.jakemarsden.git-hooks' version '0.0.1'
  id 'com.github.sherter.google-java-format' version '0.8'
  id 'com.gradle.plugin-publish' version '0.11.0'
  id 'java-gradle-plugin'
  id 'maven-publish'
}

group 'com.github.jakemarsden'
version file('VERSION').text.trim()

gitHooks {
  hooks = ['pre-commit': 'check']
}

repositories {
  jcenter()
}

ext {
  hamcrestVersion = '2.2'
  junitVersion = '5.6.2'
}

dependencies {
  testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

gradlePlugin {
  plugins {
    gitHooksPlugin {
      id = 'com.github.jakemarsden.git-hooks'
      implementationClass = 'com.github.jakemarsden.githooksgradleplugin.GitHooksPlugin'
    }
  }
}

java {
  sourceCompatibility JavaVersion.VERSION_1_8
  targetCompatibility JavaVersion.VERSION_1_8
  withJavadocJar()
  withSourcesJar()
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [
      '-Werror',
      '-Xlint:all',
      '-Xlint:-processing'
  ]
  options.encoding 'UTF-8'
}

tasks.withType(Javadoc) {
  options.encoding 'UTF-8'
}

tasks.withType(Test) {
  useJUnitPlatform()
}

googleJavaFormat {
  toolVersion '1.7'
  options style: 'GOOGLE'
}

task format {
  // alias
  group tasks.googleJavaFormat.group
  dependsOn tasks.googleJavaFormat
}

pluginBundle {
  description = 'Configure shared Git hooks right from your Gradle buildscript'
  tags = ['git', 'git-hooks']
  vcsUrl = 'https://github.com/jakemarsden/git-hooks-gradle-plugin.git'
  website = 'https://github.com/jakemarsden/git-hooks-gradle-plugin'

  plugins {
    // Ensure the version is a valid semver, and is not a `-SNAPSHOT`, before giving the option to
    // publish to the Gradle plugins portal (just in case)
    if (isReleaseVersion()) {
      gitHooksPlugin {
        displayName = 'git-hooks'
      }
    }
  }
}

publishing {
  repositories {
    // Ensure the version is a valid semver, and is not a `-SNAPSHOT`, before giving the option to
    // publish to GitHub Packages (just in case)
    if (isReleaseVersion()) {
      maven {
        name = 'githubPackages'
        url = 'https://maven.pkg.github.com/jakemarsden/git-hooks-gradle-plugin'
        credentials {
          username = project.findProperty('github.actor') ?: System.getenv('GITHUB_ACTOR')
          password = project.findProperty('github.token') ?: System.getenv('GITHUB_TOKEN')
        }
      }
    }
  }
}

private boolean isReleaseVersion() {
  project.version ==~ /^[0-9]+\.[0-9]+\.[0-9]+$/
}
